plugins {
	id 'java-library'
	id 'org.quiltmc.loom' version '1.8.5'
	id 'idea'
}

version = project.mod_version + "+quilt+mc" + project.minecraft_version
group = project.mod_group

base {
	archivesName = project.mod_id
}

repositories {
	maven {
		name = 'Parchment'
		url = 'https://maven.parchmentmc.org/'
	}
}

//fabricApi {
//	configureDataGeneration {
//		client = true
//	}
//}

dependencies {
	// To change the versions see the gradle.properties file.
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	// Use official mappings + parchment for parameter names.
	mappings loom.layered() {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-${project.parchment_mappings}@zip"
	}
	// Quilt loader.
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"
	// Fabric API.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

idea {
	module {
		// Download sources/javadoc jars for dependencies.
		downloadSources = true
		downloadJavadoc = true
	}
}

processResources {
	filesMatching("fabric.mod.json") {
		expand "mod_id": project.mod_id,
			"mod_group": project.mod_group,
			"mod_version": project.mod_version,
			"version": project.version,
			"minecraft_version": "~${project.minecraft_version}",
			"loader_version": ">=${project.loader_version}",
			"fabric_version": ">=${project.fabric_version}"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	toolchain.languageVersion = JavaLanguageVersion.of(21)
}

jar {
	from("LICENSE")
}
